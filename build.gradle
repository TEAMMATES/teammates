apply plugin: "war"
apply plugin: "appengine"
apply plugin: "eclipse"

def appengineVersion = "1.9.27"
def numOfTestRetries = 5
def isTravis = System.getenv("TRAVIS") != null
def isWindows = System.getProperty("os.name").toLowerCase().contains("windows")

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.appengine:gradle-appengine-plugin:1.9.30"
    }
}

configurations.all {
    resolutionStrategy {
        force "commons-codec:commons-codec:1.6"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    appengineSdk    "com.google.appengine:appengine-java-sdk:${appengineVersion}"

    compile         "com.fasterxml.jackson.core:jackson-core:2.1.3",
                    "com.google.api-client:google-api-client-appengine:1.18.0-rc",
                    "com.google.api-client:google-api-client:1.18.0-rc",
                    "com.google.apis:google-api-services-storage:v1beta2-rev47-1.18.0-rc",
                    "com.google.appengine.orm:datanucleus-appengine:1.0.10",
                    "com.google.appengine.tools:appengine-gcs-client:0.3.13",
                    "com.google.appengine:appengine-api-1.0-sdk:${appengineVersion}",
                    "com.google.appengine:appengine-endpoints-deps:${appengineVersion}",
                    "com.google.appengine:appengine-endpoints:${appengineVersion}",
                    "com.google.appengine:appengine-jsr107cache:${appengineVersion}",
                    "com.google.code.gson:gson:2.2.2",
                    "com.google.guava:guava:15.0",
                    "commons-fileupload:commons-fileupload:1.3.1",
                    "javax.jdo:jdo2-api:2.3-eb",
                    "org.apache.geronimo.specs:geronimo-jpa_3.0_spec:1.1.1",
                    "org.apache.geronimo.specs:geronimo-jta_1.1_spec:1.1.1",
                    "org.datanucleus:datanucleus-core:1.1.5",
                    "org.datanucleus:datanucleus-jpa:1.1.5",
                    "xerces:xercesImpl:2.9.1"

    providedCompile "com.google.appengine:appengine-api-labs:${appengineVersion}",
                    "com.google.appengine:appengine-api-stubs:${appengineVersion}",
                    "com.google.appengine:appengine-remote-api:${appengineVersion}",
                    "com.google.appengine:appengine-testing:${appengineVersion}",
                    "httpunit:httpunit:1.7",
                    "javax.mail:mail:1.4.5",
                    "org.testng:testng:6.8.7"

    testRuntime     "com.google.appengine:appengine-tools-sdk:${appengineVersion}",
                    "org.seleniumhq.selenium:selenium-java:2.46.0"


    // Hack to get selenium-server-standalone from URL
    def f = new File("libs/selenium-server-standalone-2.46.jar")
    f.getParentFile().mkdirs()
    if (!f.exists()) {
        new URL("http://selenium-release.storage.googleapis.com/2.46/selenium-server-standalone-2.46.0.jar")
            .withInputStream{i -> f.withOutputStream{ it << i }}
    }
    providedCompile fileTree(dir: "libs", include: ["*.jar"])
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir "src/test/java"
            include "**/*.java"
        }
        resources {
            srcDir "src/main/resources"
            srcDir "src/test/resources"
            exclude "**/*.java"
        }
    }
}

appengine {
    httpPort = 8888
    downloadSdk = true
    jvmFlags = ["-Duser.timezone=UTC-Xss2m"]
    enhancer {
        version = "v1"
        api = "JPA"
        enhanceOnBuild = true
    }
    appcfg {
        oauth2 = true
    }
}

appengineRun {
    daemon = true
}

(1..numOfTestRetries + 1).each { id ->
    task "killFirefox${id}"(type: Exec) {
        if (isWindows) {
            commandLine "Taskkill", "/F", "/IM", "firefox.exe"
        } else {
            commandLine "pkill", "firefox"
        }
        ignoreExitValue = true
        outputs.upToDateWhen { false }
    }
}

def afterTestClosure = { descriptor, result ->
    if (result.resultType == TestResult.ResultType.FAILURE && result.exception != null && result.exception.getMessage() != null) {
        def msg = result.exception.getMessage()
        def shouldDiff = msg.indexOf("<<[expected>\n") != -1
        if (shouldDiff) {
            new File("expected").text = msg[(msg.indexOf("<<[expected>\n") + 13)..(msg.indexOf("\n</expected]>>") - 1)]
            new File("actual").text = msg[(msg.indexOf("<<[actual>\n") + 11)..(msg.indexOf("\n</actual]>>") - 1)]
            if (isWindows) {
                def process = "FC expected actual".execute()
            } else {
                def process = "diff expected actual".execute()
            }
            println "Diff for " + descriptor.getClassName() + "." + descriptor.getName() + ":\n"
            println process.getText()
            process.waitFor()
        }
    }
}

test {
    useTestNG()
    options.suites isTravis ? "src/test/testng-travis.xml" : "src/test/testng.xml"
    options.useDefaultListeners = true
    options.outputDirectory = file("build/reports/test-try-1")
    maxHeapSize = "1g"
    ignoreFailures = numOfTestRetries != 0 ? true : false
    reports.html.enabled = false
    reports.junitXml.enabled = false
    afterTest afterTestClosure 
    finalizedBy = ["killFirefox1"]
    testLogging {
        events "passed"
    }
    if (isTravis) {
        jvmArgs "-Duser.timezone=UTC-Xss2m",  "-Djava.io.tmpdir=" + System.getenv("TRAVIS_BUILD_DIR")
    } else {
        jvmArgs "-Duser.timezone=UTC-Xss2m"
    }
    dependsOn "appengineEnhance"
}

task appengineTest {
    dependsOn "test"
}

(1..numOfTestRetries).each { id ->
    def isLastRetry = id == numOfTestRetries ? true : false
    task "testRetry${id}"(type: Test) {
        dependsOn "test", "appengineEnhance"
        useTestNG()
        options.suites("build/reports/test-try-${id}/testng-failed.xml")
        options.outputDirectory = file("build/reports/test-try-${id+1}")
        options.useDefaultListeners = true
        testClassesDir = sourceSets.test.output.classesDir
        classpath = sourceSets.test.runtimeClasspath
        testSrcDirs = sourceSets.test.java.srcDirs as List
        maxHeapSize = "1g"
        ignoreFailures = isLastRetry ? false : true
        reports.html.enabled = false
        reports.junitXml.enabled = false
        finalizedBy = isLastRetry ? ["killFirefox${id+1}", "appengineStop"] : ["killFirefox${id+1}"]
        testLogging.events = ["passed"]
        testLogging.exceptionFormat = isLastRetry ? "full" : "short"
        afterTest afterTestClosure
        onlyIf {
            file("build/reports/test-try-${id}/testng-failed.xml").exists()
        }
        if (isTravis) {
            jvmArgs "-Duser.timezone=UTC-Xss2m",  "-Djava.io.tmpdir=" + System.getenv("TRAVIS_BUILD_DIR")
        } else {
            jvmArgs "-Duser.timezone=UTC-Xss2m"
        }
    }
    appengineTest.dependsOn "testRetry${id}"
}

defaultTasks "appengineRun", "appengineTest", "appengineStop"

def testTasksMap = [
    "rare": "rare-tests",
    "component": "component-tests",
    "occasional": "occasional-tests",
    "staging": "component-tests,sequential-ui-tests,parallel-ui-tests"
]
testTasksMap.each { taskNamePrefix, testNames ->
    task "${taskNamePrefix}Tests"(type: JavaExec) {
        classpath = sourceSets.test.runtimeClasspath
        main = "org.testng.TestNG"
        args "-configfailurepolicy", "continue"
        args "-testnames", "$testNames"
        args "-reporter", "org.testng.reporters.SuiteHTMLReporter"
        args "src/test/testng.xml"
        errorOutput = {
            def f = File.createTempFile("err", null)
            f.deleteOnExit()
            f.newOutputStream()
        }()
        dependsOn "appengineRun", "cleanTestOutput"
        finalizedBy "appengineStop", "killFirefox1", "testOutput"
    }
}

task cleanTestOutput {
    def testOutputDir = new File("test-output")
    testOutputDir.deleteDir()
    outputs.upToDateWhen { false }
}

task testOutput(type: Exec) {
    onlyIf {
        file("test-output/testng-failed.xml").exists()
    }
    if (isWindows) {
        commandLine "cmd", "/c", "start", "test-output/emailable-report.html"
    } else {
        commandLine "open", "-a", "Google Chrome", "test-output/emailable-report.html"
    }
    ignoreExitValue = true
    outputs.upToDateWhen { false }
}

task singleTest(type: JavaExec) {
    onlyIf {
        project.hasProperty("methods") || project.hasProperty("classes")
    }
    classpath = sourceSets.test.runtimeClasspath
    main = "org.testng.TestNG"
    args "-configfailurepolicy", "continue"
    args "-reporter", "org.testng.reporters.SuiteHTMLReporter"
    if (project.hasProperty("testClasses")) {
        args "-testclass", testClasses
    }
    if (project.hasProperty("testMethods")) {
        args "-methods", testMethods
    }
    errorOutput = {
        def f = File.createTempFile("err", null)
        f.deleteOnExit()
        f.newOutputStream()
    }()
    dependsOn "appengineRun", "cleanTestOutput"
    finalizedBy "appengineStop", "killFirefox1", "testOutput"
}
